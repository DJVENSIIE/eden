plugins {
    id 'idea'
    id 'maven-publish'
    id 'java'
    id 'java-library'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'uk.co.bithatch:FX-BorderlessScene:5.0.3'
    implementation 'org.jetbrains:annotations:21.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

javafx {
    version = "16"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

/////////////////////////////////
/////////// VARIABLES ///////////
/////////////////////////////////

group = 'com.lgs'
version = '1.0'
description = 'Legendary Games Studio launcher'

/////////////////////////////
///////////// TASKS /////////////
/////////////////////////////////

// idea clean

idea {
    module {
        excludeDirs += [file("gradle"), file("gradlew"), file("gradlew.bat"),
                        file(".github"), file("LICENSE"), file("pom.xml")

        ]
    }
}

// compile java

application {
    mainClassName = "com.lgs.eden.Main"
}

//noinspection GroovyAssignabilityCheck
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.encoding = "UTF-8"
    // sourceCompatibility = 1.16
    // sourceCompatibility = 11
    // targetCompatibility = 11
}

// tests
test { useJUnitPlatform() }

// jar
task makeJar(type: Jar) {
    manifest {
        attributes(
                'Main-Class': application.mainClassName,
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    setArchivesBaseName(project.name)
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    destinationDirectory.set(file("$buildDir/../gen/"))
    with jar
}